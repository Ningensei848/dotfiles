# 参考文献 -----------------------------------------------------------------------
# 究極のzshプラグイン読み込み高速化: プラグインマネージャーの限界を越えろ - zenn.dev
# cf. https://zenn.dev/fuzmare/articles/zsh-plugin-manager-cache
# ----------------------------------------------------------------------------

# sheldon --------------------------------------------------------------------
# --- source command override technique
export ZSHRC_DIR="${${(%):-%N}:A:h}/.zsh"

function source {
  ensure_zcompiled $1
  builtin source $1
}

function ensure_zcompiled {
  local compiled="$1.zwc"
  if [[ ! -r "$compiled" || "$1" -nt "$compiled" ]]; then
    echo "\033[1;36mCompiling\033[m $1"
    zcompile $1
  fi
}

ensure_zcompiled ${${(%):-%N}:A:h}/.zshrc

source $ZSHRC_DIR/nonlazy.zsh

# load cache technique -------------------------------------------------------
export SHELDON_CONFIG_DIR="$ZSHRC_DIR/sheldon"
export STARSHIP_CONFIG_DIR="$ZSHRC_DIR/starship"
export STARSHIP_CONFIG="$STARSHIP_CONFIG_DIR/config.toml"

# --- from sheldon plugins
sheldon_cache="$SHELDON_CONFIG_DIR/sheldon.zsh"
sheldon_toml="$SHELDON_CONFIG_DIR/plugins.toml"
if [[ ! -r "$sheldon_cache" || "$sheldon_toml" -nt "$sheldon_cache" ]]; then
  sheldon source > $sheldon_cache
fi
source "$sheldon_cache"
unset sheldon_cache sheldon_toml

# --- from starship prompt
starship_cache="$STARSHIP_CONFIG_DIR/starship.zsh"
# starshipコマンドのパスを取得
starship_bin="$(command -v starship)"

# キャッシュ再生成条件:
# 1. キャッシュがない
# 2. 設定ファイル(config.toml)の方が新しい
# 3. starship本体(バイナリ)の方が新しい (アプデ時用)
if [[ ! -r "$starship_cache" || "$STARSHIP_CONFIG" -nt "$starship_cache" || "$starship_bin" -nt "$starship_cache" ]]; then
  starship init zsh > "$starship_cache"
fi
source "$starship_cache"
unset starship_cache starship_bin

# ----------------------------------------------------------------------------
# Finally, ...
zsh-defer zsh-defer unfunction source
